buildscript {
    ext {
        kotlin_version = '1.2.50'
        junit_version = '5.2.0'
    }

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.20.0"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.3'
    }
}

group 'me.avo'
version '1.0.2'

apply plugin: "com.github.ben-manes.versions"
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit_version
    testCompile group: 'org.amshove.kluent', name: 'kluent', version: '1.38'
    testCompile "org.jmockit:jmockit:1.40"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit_version"
}

test {
    useJUnitPlatform {
        excludeTags 'slow'
        includeEngines 'junit-jupiter'
    }

    testLogging {
        events "failed"
        exceptionFormat "full"
    }

    failFast = false
}



task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publish = true
    publications = ['MyPublication']
    configurations = ['archives']
    pkg {
        repo = 'arkenv'
        name = 'arkenv'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = "https://gitlab.com/apurebase/arkenv.git"
        version {
            name = project.version
            released = new Date()
        }
    }

    publishing {
        publications {
            MyPublication(MavenPublication) {
                from components.java
                groupId 'com.apurebase'
                artifactId 'arkenv'
                artifact sourcesJar
                version version
            }
        }
    }
}
