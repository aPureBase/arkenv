plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.40"
    id "com.github.ben-manes.versions" version "0.21.0"
    id "com.jfrog.bintray" version "1.8.4"
    id "io.gitlab.arturbosch.detekt" version "1.0.0-RC15"
    id "java"
    id "jacoco"
    id "maven-publish"
    id "com.github.dcendents.android-maven" version "2.1"
}

allprojects {
    version '2.0.0'
    group 'com.apurebase'

    apply plugin: 'kotlin'
    apply plugin: 'jacoco'
    apply plugin: "io.gitlab.arturbosch.detekt"
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'com.github.dcendents.android-maven'

    repositories {
        jcenter()
    }

    ext {
        junit_version = '5.3.2'
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        testCompile "org.yaml:snakeyaml:1.24"
        testCompile "org.jmockit:jmockit:1.46"
        testCompile group: 'org.amshove.kluent', name: 'kluent', version: '1.51'
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit_version
        testCompile "io.strikt:strikt-core:0.21.1"
        testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    }

    detekt {
        filters = ".*/resources/.*,.*/build/.*"
        config = files("../detekt.yml")
        input = files("src/main/kotlin")
    }

    test {
        useJUnitPlatform {
            excludeTags 'slow'
            includeEngines 'junit-jupiter'
        }

        testLogging {
            events "failed"
            exceptionFormat "full"
        }

        failFast = false

        doFirst {
            def jMockit = configurations.testCompile.find {
                it.name.startsWith("jmockit-")
            }
            jvmArgs "-javaagent:${jMockit}"
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        publish = true
        publications = ['MyPublication']
        configurations = ['archives']
        pkg {
            repo = 'apurebase'
            name = 'arkenv'
            licenses = ['Apache-2.0']
            vcsUrl = 'git@gitlab.com:apurebase/arkenv.git'
            websiteUrl = 'https://gitlab.com/apurebase/arkenv'
            issueTrackerUrl = 'https://gitlab.com/apurebase/arkenv/issues'
            version {
                name = project.version
                released = new Date()
            }
        }

        publishing {
            publications {
                MyPublication(MavenPublication) {
                    from components.java
                    groupId 'com.apurebase'
                    artifactId 'arkenv'
                    artifact sourcesJar
                    version version
                }
            }
        }
    }

    check.dependsOn jacocoTestReport
    check.dependsOn jacocoTestCoverageVerification
    tasks.test.finalizedBy jacocoTestReport

    jacoco {
        toolVersion = "0.8.1"
        reportsDir = file("$buildDir/reports")
    }

    jacocoTestReport {
        group = "Reporting"
        description = "Generate Jacoco coverage reports for Debug build"

        reports {
            html.enabled = true
            csv.enabled = true
        }

        // what to exclude from coverage report
        // UI, "noise", generated classes, platform classes, etc.
        def excludes = [
                '**/R.class',
                '**/R$*.class',
                '**/Manifest*.*',
                '**/*Test*.*'
        ]
        // generated classes
        afterEvaluate {
            classDirectories = project.files(classDirectories.files.collect {
                fileTree(dir: it, excludes: excludes)
            })
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.5
                }
            }

            rule {
                enabled = false
                element = 'CLASS'
                includes = ['org.gradle.*']

                limit {
                    counter = 'LINE'
                    value = 'TOTALCOUNT'
                    maximum = 0.3
                }
            }
        }
    }
}

